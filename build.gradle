plugins {
    id 'java'
    id 'antlr'
    id 'com.github.sherter.google-java-format' version '0.8'
    id 'idea'
    id 'eclipse'
    id 'org.unbroken-dome.test-sets' version '2.0.3'
    id 'application'
}

repositories {
    jcenter()
}

sourceCompatibility = 10
targetCompatibility = 10

testSets {
    integrationTest
}

dependencies {
    antlr 'org.antlr:antlr4:4.7.1'

    compileOnly 'org.projectlombok:lombok:1.18.4'
    annotationProcessor 'org.projectlombok:lombok:1.18.4'

    implementation 'org.ow2.asm:asm:7.0'
    implementation 'org.ow2.asm:asm-util:7.0'

    implementation 'com.beust:jcommander:1.72'

    testCompileOnly 'org.projectlombok:lombok:1.18.4'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.4'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.3.1'

    testImplementation 'com.fasterxml.jackson.core:jackson-databind:2.9.7'
    testImplementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.9.7'

    testImplementation 'org.skyscreamer:jsonassert:1.5.0'

    testImplementation 'com.google.guava:guava:27.0-jre'

    integrationTestImplementation sourceSets.test.output

    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.1'
}

generateGrammarSource {
    arguments += ['-no-listener', '-package', 'com.github.agmcc.slate.antlr']
    outputDirectory = new File("$buildDir/generated-src/antlr/main/com/github/agmcc/slate/antlr")
}

task format {
    dependsOn ':googleJavaFormat'
    group 'build'
    description 'Google Java Format'
}

tasks.withType(Test) {
    useJUnitPlatform()
    testLogging {
        events 'failed', 'standard_error'
        exceptionFormat = 'full'
    }
}

check.dependsOn integrationTest
integrationTest.mustRunAfter(test)

application {
    mainClassName 'com.github.agmcc.slate.cli.SlateCli'
    applicationName 'slatec'
}
