plugins {
    id 'idea'
    id 'com.github.sherter.google-java-format' version '0.8'
}

allprojects {
    repositories {
        jcenter()
    }
}

subprojects {
    apply plugin: 'java-library'

    ext {
        lombokVersion = '1.18.8'
        daggerVersion = '2.23.1'
        junitVersion = '5.4.2'
        jacksonVersion = '2.9.9'
    }

    sourceCompatibility = 12
    targetCompatibility = 12

    dependencies {
        /* PRODUCTION */

        compileOnly "org.projectlombok:lombok:$lombokVersion"
        annotationProcessor "org.projectlombok:lombok:$lombokVersion"

        implementation "com.google.dagger:dagger:$daggerVersion"
        annotationProcessor "com.google.dagger:dagger-compiler:$daggerVersion"

        implementation 'org.apache.logging.log4j:log4j-core:2.12.0'

        runtimeOnly "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"
        runtimeOnly "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:$jacksonVersion"

        /* TEST */

        testCompileOnly "org.projectlombok:lombok:$lombokVersion"
        testAnnotationProcessor "org.projectlombok:lombok:$lombokVersion"

        testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
        testImplementation "org.junit.jupiter:junit-jupiter-params:$junitVersion"

        testAnnotationProcessor "com.google.dagger:dagger-compiler:$daggerVersion"

        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
    }

    tasks.withType(Test) {
        useJUnitPlatform()
        testLogging {
            events 'failed', 'standard_error'
            exceptionFormat = 'full'
        }
    }
}

googleJavaFormat {
    toolVersion = '1.7'
}

task format {
    dependsOn ':googleJavaFormat'
    group 'build'
    description 'Google Java Format'
}
